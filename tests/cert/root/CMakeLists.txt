# Generate the ROOT CA
add_custom_command(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/ca_root_encrypted.key
                   COMMAND
                   ${CMAKE_COMMAND}
                   -E remove -f ${CMAKE_CURRENT_BINARY_DIR}/ca_root_encrypted.key
                   COMMAND
                   ${QUIET_OPENSSL_BINARY}
                   rsa
                   -passout file:${CMAKE_CURRENT_SOURCE_DIR}/passphrase.txt
                   -des3
                   -in ${CMAKE_CURRENT_BINARY_DIR}/ca_root.key
                   -out ${CMAKE_CURRENT_BINARY_DIR}/ca_root_encrypted.key.tmp
                   COMMAND
                   ${CMAKE_COMMAND}
                   -E copy ${CMAKE_CURRENT_BINARY_DIR}/ca_root_encrypted.key.tmp
                   ${CMAKE_CURRENT_BINARY_DIR}/ca_root_encrypted.key
                   DEPENDS passphrase.txt ${CMAKE_CURRENT_BINARY_DIR}/ca_root.key
                   COMMENT "Encrypt CA Root private key")

add_custom_command(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/ca_root.cert
                   ${CMAKE_CURRENT_BINARY_DIR}/ca_root.key
                   COMMAND
                   ${CMAKE_COMMAND}
                   -E remove -f ${CMAKE_CURRENT_BINARY_DIR}/ca_root.cert ${CMAKE_CURRENT_BINARY_DIR}/ca_root.key
                   COMMAND
                   ${QUIET_OPENSSL_BINARY}
                   req
                   -new
                   -x509
                   -days 365
                   -newkey rsa:${OPENSSL_NUMBER_OF_BITS}
                   -${OPENSSL_SIGNING_ALGORITHM}
                   -keyout ${CMAKE_CURRENT_BINARY_DIR}/ca_root.key.tmp
                   -config ${CMAKE_CURRENT_SOURCE_DIR}/config
                   -out ${CMAKE_CURRENT_BINARY_DIR}/ca_root.cert.tmp
                   COMMAND
                   ${CMAKE_COMMAND}
                   -E copy ${CMAKE_CURRENT_BINARY_DIR}/ca_root.key.tmp
                   ${CMAKE_CURRENT_BINARY_DIR}/ca_root.key
                   COMMAND
                   ${CMAKE_COMMAND}
                   -E copy ${CMAKE_CURRENT_BINARY_DIR}/ca_root.cert.tmp
                   ${CMAKE_CURRENT_BINARY_DIR}/ca_root.cert
                   DEPENDS
                   ${CMAKE_CURRENT_SOURCE_DIR}/config
                   COMMENT "Generate CA Root X.509 certificate")

add_custom_target(Generate_kv_engine_X509_Test_Root_Certificate
                  DEPENDS
                  ${CMAKE_CURRENT_BINARY_DIR}/ca_root.key
                  ${CMAKE_CURRENT_BINARY_DIR}/ca_root_encrypted.key
                  ${CMAKE_CURRENT_BINARY_DIR}/ca_root.cert)

add_custom_target(Dump_kv_engine_X509_Test_Root_Certificate
                  COMMAND
                  ${OPENSSL_BINARY}
                  x509
                  -in ${CMAKE_CURRENT_BINARY_DIR}/ca_root.cert
                  -text
                  DEPENDS
                  Generate_kv_engine_X509_Test_Root_Certificate
                  )
